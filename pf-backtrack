#!/usr/bin/perl

##
# Partition Function Backtrack (v0.1.0)
# Epigenetics Unit @ HuGeF [Human Genetics Foundation]
#
# Author:  Danny Incarnato (danny.incarnato[at]hugef-torino.org)
# Summary: Uses the ViennaRNA package to calculate partition function for a given RNA, and returns a 
#          sample of secondary structures from the Boltzmann ensemble according to their probability
#
# This program is free software, and can be redistribute  and/or modified
# under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# any later version.
#
# Please see <http://www.gnu.org/licenses/> for more informations.
##

use strict;
use FindBin qw($Bin);
use Getopt::Long qw(:config no_ignore_case);
use RNA;

use lib $Bin . "/lib";

use Core::Mathematics;
use Data::Sequence::Utils;
use RNA::Utils;

my ($sequence, $constraint, $t, $nlp,
    $ngu, $sample, $help);
$t = 37.0;
$sample = 1000;

do {
    
    local $SIG{__WARN__} = sub { };

    GetOptions( "h|help"               => \$help,
                "s|sequence=s"         => \$sequence,
                "c|constraint=s"       => \$constraint,
                "t|temperature=s"      => \$t,
                "nlp|no-lonely-pairs"  => \$RNA::noLonelyPairs,
                "ngu|no-closing-gu"    => \$RNA::no_closingGU,
                "b|boltzmann-sample=i" => \$sample ) or help(1);

};

help() if ($help);

die "\n  [!] Error: No RNA sequence specified\n\n" unless(defined $sequence);
die "\n  [!] Error: Sequence contains invalid characters\n\n" unless(isna($sequence));
die "\n  [!] Error: Constraint contains invalid characters\n\n" if (defined $constraint &&
                                                                    !isdotbracket($constraint, "x"));
die "\n  [!] Error: Sequence and constraint have different lengths\n\n" if (defined $constraint &&
                                                                            length($sequence) == length($constraint));
die "\n  [!] Error: Temperature must be a positive value\n\n" unless(ispositive($t));
die "\n  [!] Error: Temperature must be comprised between 0 and 100 degree Celsius\n\n" if ($t > 100);
die "\n  [!] Error: Boltzmann sample size value must be integer\n\n" unless(isint($sample));
die "\n  [!] Error: Boltzmann sample size must be comprised between 1 and 1,000,000\n\n" if ($sample < 1 ||
                                                                                             $sample > 1000000);
$sequence = dna2rna($sequence);
$RNA::temperature = $t;
$RNA::st_back = 1;
$RNA::fold_constrained = 1 if ($constraint);

if (length($sequence) > 1000) {
                
    my ($mfe, $kT, $pfscale);
    $kT = ($RNA::temperature + 273.15) * 1.98717 / 1000.;
    
    if ($RNA::fold_constrained) { (undef, $mfe) = RNA::fold($sequence, $constraint); }
    else { (undef, $mfe) = RNA::fold($sequence); }
    
    $pfscale = exp(-(1.07 * $mfe) / $kT / length($sequence));
    $RNA::pf_scale = $pfscale < 1 ? 1 : $pfscale;
    
    RNA::free_arrays();
                
}

if ($RNA::fold_constrained) { RNA::pf_fold($sequence, $constraint); }
else { RNA::pf_fold($sequence); }

print RNA::pbacktrack($sequence) . "\n" for (1 .. $sample);
RNA::free_pf_arrays();

sub help {
    
    print "\n  [!] Error: Invalid option. Please check the help\n" if ($_[0]);
    
    die <<HELP;
 
 Partition Function Backtrack (v0.1.0)
 Epigenetics Units @ HuGeF [Human Genetics Foundation]
 Group leader: Prof. Salvatore Oliviero
    
 Author:  Danny Incarnato (danny.incarnato[at]hugef-torino.org)
 Summary: Uses the ViennaRNA package to calculate partition function for a given RNA, and returns a
          sample of secondary structures from the Boltzmann ensemble according to their probability
 
 Usage:   pf-backtrack [Options]
 
 Options                             Description
 -s   or --sequence                  RNA sequence to fold
 -c   or --constraint                Defines an hard constraint to guide RNA's folding
 -b   or --boltzmann-sample          Number of structures to sample from the Boltzmann ensemble (1-1000000, Default: 1000)
 -t   or --temperature               Temperature in Celsius degrees (0-100, Default: 37.0)
 -nlp or --no-lonely-pairs           Disallows lonely (unstacked) base-pairs inside predicted structure
 -ngu or --no-closing-gu             Disallows G:U wobbles at the end of helices
 
HELP
    
}